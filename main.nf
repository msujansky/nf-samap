#!/usr/env/bin nextflow

/*
 *  PIPELINE: main.nf
 *
 *  Description:
 *      SAMap-based cross-species transcriptome mapping pipeline.
 *      Performs preprocessing of input metadata, reciprocal BLAST between
 *      species, SAMap alignment, and visualization of results.
 *
 *  Inputs:
 *      - data/transcriptomes/*.fasta      Input transcriptome FASTA files
 *      - data/*.h5ad                      Precomputed AnnData files
 *      - sample_sheet.csv                 Sample metadata sheet
 *
 *  Workflow Overview:
 *      1. Preprocess sample sheet to classify inputs and assign IDs
 *      2. Generate all unordered species pairs
 *      3. Run reciprocal BLAST on each species pair
 *      4. Build a SAMap object
 *      5. Run SAMap on each BLAST result
 *      6. Visualize SAMap alignment and write results
 *
 *  Parameters:
 *      --run_id        Run ID provided by user. If none is provided a timestamp is used. Default: null
 *      --sample_sheet  Path to the sample sheet provided by user. Default: 'sample_sheet.csv'
 *      --data_dir      Path to the directory containing the data. Default: 'data'
 *      --maps_dir      Path to a directory containing precomputed BLAST maps if any are provided. 
 *                      Any value other than null will skip the BLAST module. Default: null
 *      --results_dir   The directory all where all results will be stored. Default: 'results'
 *
 *  Outputs:
 *      - results_dir/run_id/sample_sheet.csv       Updated metadata with type and ID
 *      - results_dir/run_id/maps/                  Reciprocal BLAST outputs per sample pair
 *      - results_dir/run_id/samap_objects/         Pickled SAMap object (Python)
 *      - results_dir/run_id/plots/chord.html       Chord plot
 *      - results_dir/run_id/plots/sankey.html      Mapping Sankey diagram
 *      - results_dir/run_id/plots/scatter.png      Gene mapping scatter plot
 *      - results_dir/run_id/csv/hms.csv            Highest mapping scores (HMS) matrix
 *      - results_dir/run_id/csv/pms.csv            Pairwise mapping scores (PMS) matrix
 *      - results_dir/run_id/logs/*.log             Logfiles generated by the modules.
 *
 *  Author:     Ryan Sonderman
 *  Created:    2025-06-12
 *  Version:    1.0.0
 */

// Import the required modules 
include { PREPROCESS } from './modules/preprocess.nf'
include { RUN_BLAST_PAIR } from './modules/run_blast_pair.nf'
include { LOAD_SAMS } from './modules/load_sams.nf'
include { BUILD_SAMAP } from './modules/build_samap.nf'
include { RUN_SAMAP } from './modules/run_samap.nf'
include { VISUALIZE_SAMAP } from './modules/visualize_samap.nf'

workflow {
    // Generate run ID unless one is provided
    run_id = params.run_id ?: "${new Date().format('yyyyMMdd_HHmmss')}"
    run_id_ch = Channel.value(run_id)

    // Stage static input files
    data_dir        = Channel.fromPath(params.data_dir)
    results_dir     = Channel.fromPath(params.results_dir)
    sample_sheet    = Channel.fromPath(params.sample_sheet)

    // Validation of necessary files
    if (!new File(params.sample_sheet).exists()) {
        error "Missing required file: sample sheet '${params.sample_sheet}'"
    }


    // Preprocess sample sheet to add type and ID
    PREPROCESS(
        run_id_ch,
        sample_sheet,
        data_dir,
    )
    sample_sheet_pr = PREPROCESS.out.sample_sheet_pr


    // Generate unique unordered sample pairs
    samples_channel = sample_sheet_pr.splitCsv(header: true, sep: ',')
    pairs_channel = samples_channel
        .combine(samples_channel)
        .filter { a, b -> a.id2 < b.id2 }


    // Run BLAST or load precomputed map files 
    if (params.maps_dir) {
        // Use user-supplied BLAST maps
        maps_dir = Channel.fromPath(params.maps_dir)
    } else {
        // Run BLAST and extract parent maps directory
        blast_maps = RUN_BLAST_PAIR(
            run_id_ch,
            pairs_channel,
            data_dir.first(),
        )
        // Set path to maps from BLAST results
        maps_dir = blast_maps
            .map { it[0].getParent().getParent() }
            .unique()
    }


    // Load SAM objects from the AnnData h5ad files
    LOAD_SAMS(
        run_id_ch,
        sample_sheet_pr,
        data_dir,
    )
    sams = LOAD_SAMS.out.sams


    // Build the SAMap object from the SAM objects and the BLAST maps
    samap = BUILD_SAMAP(
        run_id_ch,
        sample_sheet_pr,
        data_dir,
        maps_dir,
        results_dir,
        sams,
    )


    // Run SAMap on the SAMAP object to generate mapping results
    samap_results = RUN_SAMAP(
        run_id_ch,
        results_dir,
        samap,
    )


    // Visualize the SAMap results
    VISUALIZE_SAMAP(
        run_id_ch,
        samap_results,
        sample_sheet_pr,
    )
}
